plugins {
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'groovy'
}

group 'de.dfki.mary'
version '0.5.0-SNAPSHOT'
description 'Forced alignment based on MaryTTS, Kaldi, and MFA'

gradlePlugin {
    def pluginUrl = 'https://github.com/marytts/gradle-marytts-kaldi-mfa-plugin'
    website = pluginUrl
    vcsUrl = pluginUrl
    plugins {
        maryttsKaldiMfaPlugin {
            id = 'de.dfki.mary.voicebuilding.marytts-kaldi-mfa'
            implementationClass = 'de.dfki.mary.voicebuilding.MaryttsKaldiMfaPlugin'
            displayName = 'Gradle MaryTTS Kaldi MFA plugin'
            description = project.description
            tags.set(['text-to-speech', 'kaldi', 'marytts', 'forced-alignment'])
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'io.github.m2ci-msp', name: 'jtgt', version: '0.7.0'
    testImplementation group: 'org.testng', name: 'testng', version: '7.5.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withJavadocJar()
    withSourcesJar()
}

test {
    useTestNG()
    failFast = true
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}
