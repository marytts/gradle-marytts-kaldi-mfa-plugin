plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.7'
}

group 'de.dfki.mary'
version '0.2.0-SNAPSHOT'
description 'Forced alignment based on MaryTTS, Kaldi, and MFA'

ext {
    pluginId = 'de.dfki.mary.voicebuilding.marytts-kaldi-mfa'
    pluginUrl = 'https://github.com/marytts/gradle-marytts-kaldi-mfa-plugin'
}

gradlePlugin {
    plugins {
        maryttsKaldiMfaPlugin {
            id = pluginId
            implementationClass = 'de.dfki.mary.voicebuilding.MaryttsKaldiMfaPlugin'
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile group: 'de.undercouch', name: 'gradle-download-task', version: '3.3.0'
    compile group: 'org.m2ci.msp', name: 'jtgt', version: '0.5.3'
    runtime group: 'de.dfki.mary', name: 'marytts-voicebuilding', version: '0.1', {
        exclude module: 'groovy-all'
    }
    runtime group: 'de.dfki.mary', name: 'marytts-lang-en', version: '5.2', {
        exclude module: 'groovy-all'
    }
    testCompile group: 'org.testng', name: 'testng', version: '6.9.6'
}

test {
    useTestNG()
    testLogging {
        exceptionFormat = 'full'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
}

pluginBundle {
    website = pluginUrl
    vcsUrl = pluginUrl
    description = project.description
    tags = ['text-to-speech', 'kaldi', 'marytts', 'forced-alignment']

    plugins {
        maryttsKaldiMfaPlugin {
            id = pluginId
            displayName = 'Gradle MaryTTS Kaldi MFA plugin'
        }
    }
}
