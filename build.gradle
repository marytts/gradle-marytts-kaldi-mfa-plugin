plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.21.0'
}

group 'de.dfki.mary'
version '0.4-SNAPSHOT'
description 'Forced alignment based on MaryTTS, Kaldi, and MFA'

pluginBundle {
    def pluginUrl = 'https://github.com/marytts/gradle-marytts-kaldi-mfa-plugin'
    website = pluginUrl
    vcsUrl = pluginUrl
    description = project.description
    tags = ['text-to-speech', 'kaldi', 'marytts', 'forced-alignment']
}

gradlePlugin {
    plugins {
        maryttsKaldiMfaPlugin {
            id = 'de.dfki.mary.voicebuilding.marytts-kaldi-mfa'
            implementationClass = 'de.dfki.mary.voicebuilding.MaryttsKaldiMfaPlugin'
            displayName = 'Gradle MaryTTS Kaldi MFA plugin'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'io.github.m2ci-msp', name: 'jtgt', version: '0.7.0'
    testImplementation group: 'org.testng', name: 'testng', version: '7.5'
}

test {
    useTestNG()
    failFast = true
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
}
